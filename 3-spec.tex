\documentclass{article}
\usepackage{listings}
\lstloadlanguages{Haskell}
\lstnewenvironment{code}
    {\lstset{}%
      \csname lst@SetFirstLabel\endcsname}
    {\csname lst@SaveFirstLabel\endcsname}
    \lstset{
      basicstyle=\small\ttfamily,
      flexiblecolumns=false,
      basewidth={0.5em,0.45em},
      literate={+}{{$+$}}1 {/}{{$/$}}1 {*}{{$*$}}1 {=}{{$=$}}1
               {>}{{$>$}}1 {<}{{$<$}}1 {\\}{{$\lambda$}}1
               {\\\\}{{\char`\\\char`\\}}1
               {->}{{$\rightarrow$}}2 {>=}{{$\geq$}}2 {<-}{{$\leftarrow$}}2
               {<=}{{$\leq$}}2 {=>}{{$\Rightarrow$}}2 
               {\ .}{{$\circ$}}2 {\ .\ }{{$\circ$}}2
               {>>}{{>>}}2 {>>=}{{>>=}}2
               {|}{{$\mid$}}1               
    }
\begin{document}

\section{The specification}

We shall give a specification for fixed point arithmetic in two
steps.  First, we shall specify unbounded exact arithmetic, and
then we shall specify a framework for converting fixed point
numbers to ``fixed point frames''.  The specification is executable
code in the programming language Haskell [ref].

One specification covers both binary and decimal fixed point
arithmetic.  The base is part of the type of a fixed point number.
We need a popular extension to Haskell 2010, and we're going to
need a helper function for converting to text.  For conversion
from text, we need to recognise digits.
\begin{code}
{-# LANGUAGE ScopedTypeVariables #-}
import Numeric (showSigned)
import Data.Char (isDigit)
\end{code}
Binary and Decimal are ``empty types'' with no values other
than $\bot$ used as compile-time marks.  The type class
Has\_Radix exists to convert these marks to run-time values.
\begin{code}
data Binary
data Decimal

class Has_Radix t
  where radix :: t -> Integer

instance Has_Radix Binary
  where radix _ = 2

instance Has_Radix Decimal
  where radix _ = 10
\end{code}

Fixed\_Point $t$ is a ``phantom type'', so called because it
contains no run-time value of type $t$.  The values it does
contain are an integer $m$ and a scale $s$ so that it represents
the value $m\times r^s$ where $r$ is the radix determined by $t$.
These integers are unbounded; we are dealing with exact arithmetic
at this point.
\begin{code}
data Fixed_Point t = FP !Integer !Integer
\end{code}

From an FP value we can extract its scale and base, even though
the base is not stored.  We can also provide the LIA function
{\it ulp} (unit in last place).

\begin{code}
scale :: forall t . Has_Radix t => FixedPoint t -> Integer
scale (FP _ s) = s

base :: forall t . Has_Radix t => FixedPoint t -> Integer
base _ = radix (undefined :: t)

ulp :: Has_Radix t => FixedPoint t -> FixedPoint t
ulp (FP _ s) = FP 1 s
\end{code}

The :: line for the scale function says ``for any type $t$,
provided that $t$ belongs to the Has\_Radix class (that is,
`radix' is defined on it), scale is a function with a
Fixed\_Point argument that delivers an Integer result.
The ScopedTypeVariables feature allows us to mention $t$ in
the body of the base function.

\subsection{Comparison}

What does it mean for (FP $m_1$ $s_1$) to equal (FP $m_2$ $s_2$)?
Consider (FP 0 0) and (FP 0 1).  They both represent 0.  But they
are behaviourally different.  For example, (FP 0 0) should print
as \verb|"0"| while (FP 0 1) should print as \verb|"0.0"|.  If we
want equality to satisfy Leibniz' law, we have to regard these as
distinct.  On the other hand, it would be difficult to do numeric
calculations if zero wasn't equal to zero.

The standard mathematical way to deal with this is to distinguish
two spaces:  a space $X$ of $(m,s)$ pairs which are equal if and
only if identity and a quotient space $F = X/\equiv$ where
$(m_1,s_1) \equiv (m_2,s_2)$ if and only if $m_1\times r^{s_2} =
m_2 \times r^{s_1}$.  Haskell's \verb|==|, which we are about to
define, should be thought of as $\equiv$ rather than $=$.

In order to compare two numbers, it helps to align them so they
have their radix points in the same place.  This is also useful
for addition and subtraction.

\begin{code}
align :: forall t a . Has_Radix t =>
    Fixed_Point t -> Fixed_Point t -> (Integer -> Integer -> a) -> a

align (FP m1 s1) (FP m2 s2) f
  | s1 > s2 = f m1 (m2 * r ^ (s1-s2))
  | s1 < s2 = f (m1 * r ^ (s2-s1)) m2
  | True    = f m1 m2
  where r = radix (undefined :: t)
\end{code}

This takes two fixed-point numbers in the same base, aligns them,
and passes the aligned values to a function of integers.

By plugging Fixed\_Point into Haskell's Eq type class and
defining \verb|==|, we get \verb|/=| for free.  This provides
the LIA operations {\it eq} and {\it neq}.

\begin{code}
instance (Has_Radix t) => Eq (Fixed_Point t)
  where x1 == x2 = align x1 x2 (==)
\end{code}

By plugging Fixed\_Point into Haskell's Ord type-class and
defining three-way comparison, we get $<$, $\le$, $>$, $>=$,
max, and min for free.  This provides the LIA-1 operations {\it lss},
{\it leq}, {\it gtr}, and {\it geq} and the LIA-2 operations
{\it max} and {\it min}, and since `maximum' and `minimum' are
predefined folds derived from `max' and `min', the LIA-2
operations {\it max\_seq} and {\it min\_seq}.

\begin{code}
instance (Has_Radix t) => Ord (Fixed_Point t)
  where compare x1 x2 = align x1 x2 compare
\end{code}

This is all we need to extend LIA-2's divisibility test to
fixed-point numbers:
\begin{code}
lia_divides :: Has_Radix t => Fixed_Point t -> Fixed_Point t -> Bool
lia_divides x y = align x y (\m n -> m /= 0 && n`rem`m == 0)
\end{code}

% We can even define
% lia_gcd :: Has_Radix t => Fixed_Point t -> Fixed_Point t -> Fixed_Point t
% lia_gcd x y = FP (align x y gcd) (scale x`max`scale y)


\subsection{Converting numbers to text}

For general use, we would need a function to convert a
number to a specified number of decimal digits according
to a given rounding mode.  Here we simply specify conversion
to decimal.  Numbers with scale $s \le 0$ have no decimal
point; if $s > 0$ the number of decimal digits required is
$s$ whether the base is Binary or Decimal.  If we supported
bases that were not a multiple of 2, fixed-point numbers in
such a base might not have a finite decimal representation.

Plugging into Haskell's Show type-class takes care of
inserting a negative sign (and if the context demands it,
enclosing parentheses), and avoiding excess concatenation.

?LIA

\begin{code}
instance (Has_Radix t) => Show (Fixed_Point t)
  where
    showsPrec p x = showSigned showPos p x
      where showPos x@(FP m s) rest =
              if s <= 0 then shows (m * base x^negate s) rest
              else shows i ('.' : show_fract s r)
              where p = base x ^ s
                    (i,r) = quotRem m p
                    show_fract 0 _ = rest
                    show_fract d f = shows h (show_fract (d-1) l)
                      where (h,l) = quotRem (10 * f) p
\end{code}

Converting from text to numbers runs into the problem that
0.1 has no finite representation as a Fixed\_Point Binary.
We'll deal with that after considering rounding.

\subsection{Ring arithmetic}

Numbers of the form $m\times r^s$ form a ring under the usual
arithmetic operations, but they are \emph{not} a field, see
subsection Division.
We can convert a fixed-point number to an exact Rational number and
those \emph{are} a field.  None of the ring operations can fail.
This provides the LIA operations {\it add}, {\it sub}, {\it mul},
{\it neg}, {\it abs}, and {\it sign}.  The 2012 revision of LIA-1
defines {\it signum} instead of {\it sign}.  The LIA-2 {\it dim}
function can also be defined for any ordered numeric type.

\begin{code}
instance (Has_Radix t) => Num (Fixed_Point t)
  where
    x + y = FP (align x y (+)) (scale x `max` scale y)
    x - y = FP (align x y (-)) (scale x `max` scale y)
    negate (FP m s) = FP (negate m) s
    abs    (FP m s) = FP (abs    m) s
    signum (FP m s) = FP (signum m) 0
    fromInteger n   = FP n 0
    (FP m1 s1) * (FP m2 s2)  = FP (m1*m2) (s1+s2)

lia_signum :: (Ord t, Num t) => t -> t
lia_signum x = if x < 0 then -1 else 1

lia_dim :: (Ord t, Num t) => t -> t -> t
lia_dim x y = if x < y then fromInteger 0 else x - y

instance (Has_Radix t) => Real (Fixed_Point t)
  where
    toRational x@(FP m s) = toRational m / toRational (base x) ^ s
\end{code}

Since raising to a non-negative power requires only the unit
(fromInteger 1) and multiplication, we get the power operation
$x$\verb|^|$n$ free.  This operation fails if $n<0$, aligning
it with the LIA-2 {\it power${}_I$} function, but without the
breakage of making $0^0$ an error.  For example,
\begin{verbatim}
> (1234 2 :: Fixed_Point Decimal) ^ 4
23187.85835536
\end{verbatim}

\subsection{Conversion to integer}

The fromInteger function converts integers to fixed-point;
going the other way loses information so we need to say how.

In [section Rounding] we identified 14 rounding modes.
We have three options for naming them:
\begin{itemize}
\item They could be {\it values}, as done in this section.
\item They could be {\it functions}, {\it e.g.},
\begin{code}% don't copy
down :: Real t => Integer
down x = floor (toRational x)
\end{code}
We can construct these functions if we want them:
\begin{code}% don't copy
down = convert Down
\end{code}
\item They could be {\it types} like Binary and Decimal,
which would mean that the Fixed\_Frame type defined in a
later section would have less run-time content.
\end{itemize}

\begin{code}
data Rounding_Mode
   = Down
   | Up
   | In
   | Out
   | Exact
   | Even
   | Odd
   | Nearest Rounding_Mode
\end{code}

This is very nearly an enumeration type, except that it
allows constructions like Nearest Out.  (It also allows
Nearest (Nearest Odd), which is useless but harmless.)

We're going to define a single ``convert'' function that has
a rounding mode and a rational number and returns an integer.
This depends on the standard function `properFraction',
which splits a rational number into an integer part and
a fractional part of the same type as its argument, such
that both parts are non-negative or both parts are non-positive.
RealFrac is the class of numeric types on which it is defined.

\begin{code}
convert :: RealFrac t => Rounding_Mode -> t -> Integer

convert round x =
  case round of
    Down      -> if d < 0 then i-1 else i
    Up        -> if d > 0 then i+1 else i
    In        -> i
    Out       -> i + d
    Exact     -> if d == 0 then i else error "inexact"
    Even      -> if even i then i else i + d
    Odd       -> if odd  i then i else i + d
    Nearest h -> if d == 0 then i else
                 case compare (2*abs f) 1 of
                   LT -> i
                   GT -> i + d
                   EQ -> i + convert h f
  where (i,f) = properFraction x
        d     = if f < 0 then -1 else if f > 0 then 1 else 0
\end{code}

We can't plug Fixed\_Point into RealFrac that because numeric types
in Haskell can only belong to RealFrac if they also belong to
Fractional, which requires them to have division, reciprocal,
and conversion from arbitrary rationals.

This is an occasion where Haskell proves to be inconvenient,
because the properFraction, truncate, round, ceiling, and floor
functions of the RealFrac class make perfect sense for
fixed-point numbers.  However, we can compose convert with
toRational, making it possible to convert fixed-point numbers
to different scales and different radices in the spirit of LIA,
being like  the LIA functions {\it round} and {\it trunc}.

\begin{code}
rescale :: forall t r . (Has_Radix t, Real r) =>
          Rounding_Mode -> Integer -> r -> Fixed_Point t

rescale round digits x = FP (convert round y) digits
  where y = toRational x * toRational (radix (undefined :: t) ^ digits)
\end{code}

The (convertFixed In) function provides the
LIA (floating-point) operations {\it intpart}, and {\it fractpart}.

\begin{code}
convertFixed :: Has_Radix t =>
    Rounding_Mode -> Fixed_Point t -> (Integer, Fixed_Point t)

convertFixed round x = (q, x - fromInteger q)
  where q = convert round (toRational x)
\end{code}

Given this operation, we can define division with an
integer quotient and fixed-point remainder, supporting
the LIA-2 {\it quot}, {\it mod}, {\it ratio}, {\it residue},
{\it group}, and {\it pad} functions.

\begin{code}
quotient :: Has_Radix t => 
    Rounding_Mode -> Fixed_Point t -> Fixed_Point t ->
    (Integer, Fixed_Point t)
quotient round x y = (q, x - fromInteger q * y)
  where q = convert round (toRational x / toRational y)
\end{code}
% Error: was convertFixed round x   -- TWO errors!

The Haskell type-class Integral provides integer quotient
and remainder in the type-class Integral.  This is arguably
the wrong place, which explains why we have
\begin{code}%don't copy
quotient Floor -- instead of divMod
quotient In    -- instead of quotRem
\end{code}

\subsection{Frames}

In a language like COBOL or PL/I, the result of a calculation
must fit into the declared type of a variable, while the way
that the exact result is rounded to fit may be separately
specified.  In other languages, like C$\sharp$, the scale is
part of the value, but there is a maximum scale.  We shall
call the combination of radix (carried in the type),
a desired or maximal scale, a rounding
mode, and optional bounds a Fixed\_Frame, and the operation
of taking an exact result and coercing it into a frame ``fitting''.
A programming language standard could for example say that
``BINARY FIXED ($p,s$) corresponds to
Fixed\_Frame Binary (Exact\_Scale $s$) (Nearest Out)
(Just (-($2^p)$, $2^p-1$))''.

\begin{code}
data Result_Scale
   = Exact_Scale   !Integer
   | Maximum_Scale !Integer

data Fixed_Frame t
   = Fixed_Frame !Result_Scale !Rounding_Mode
                 !Maybe (Integer, Integer)

fit :: Has_Radix t =>
       Fixed_Frame t -> Fixed_Point t -> Fixed_Point t

fit (Fixed_Frame rs round bounds) x@(FP m s)
  = check_bounds bounds m' y
  where
    y@(FP m' _) =
      case rs of
        Exact_Scale e   -> if e == s then x else
                           rescale round e x
        Maximum_Scale e -> if e >= s then x else
                           rescale round e x

check_bounds :: Ord t => Maybe (t,t) -> t -> r -> r

check_bounds Nothing      _ y = y
check_bounds (Just (l,u)) x y =
  if l <= x && x <= u then y else error "overflow"

\end{code}

\subsection{Division}

COBOL, PL/I, SQL, Java, C$\sharp$, Swift, and most libraries
inspected for this work provide division.  The COBOL
DIVIDE statement can be given a satisfactory definition because
the destination and the rounding option provide a Fixed\_Frame.
PL/I's DIVIDE function also provides a Fixed\_Frame for the
answer (with only one rounding mode available).

However, no language or library has a satisfactory definition of
``/'' delivering an intermediate result without constraints
provided by a destination.  That is because no satisfactory
definition exists.

The set of fixed-point numbers is not closed under division.
Consider 0.1/0.3.  This has an exact rational result, 1/3,
but there is no $s$ such that $1/3 \times 10^s$ is an integer.
So unlike addition, subtraction, and multiplication, it is not
possible for division to provide an answer that is both fixed
point and exact.

The best that can be one is to specify division given a frame.

\begin{code}
divide :: forall t u v . (Has_Radix t, Real u, Real v) =>
          Fixed_Frame t -> u -> v -> Fixed_Point t
divide (Fixed_Frame rs round bounds) x y
  = check_bounds bounds m (FP m s)
  where
    s = case rs of
          (Exact_Scale   z) -> z
          (Maximum_Scale z) -> z
    p = radix (undefined :: t) ^ s
    q = toRational x / toRational y
    m = convert round (q * fromInteger p) 
\end{code}

The same problem arises when trying to define a square root
function on fixed-point numbers, as some libraries do.  The
same solution, of requiring an explicit frame.  LIA-2 extends
{\it sqrt} to integers, but only with the In rounding mode.

\subsection{Converting from text to number}

Specifying conversion for Fixed\_Point Decimal numbers
with their natural scale is tedious rather than difficult.
The code that follows is a finite state automaton that
accumulates an integer and counts digits after the decimal point.
This follows the convention of the Haskell Read class that
a string is mapped to a list of (value,residue) pairs with a
pair for each legal way to parse a prefix of the string.  In
this case there is either one way or none.

\begin{code}
readDecimalFixed :: String -> [(Fixed_Point Decimal,String)]
readDecimalFixed ('-':cs) = after_sign negate cs    
readDecimalFixed cs       = after_sign id     cs  

after_sign f (c:cs) 
  | isDigit c = after_digit f (add_digit 0 c) cs 
after_sign _ _ = []

after_digit f n (c:cs)
  | isDigit c = after_digit f (add_digit n c) cs     
after_digit f n ('.':c:cs)
  | isDigit c = after_dot f 1 (add_digit n c) cs                 
after_digit f n ('.':_) = []
after_digit f n cs = [(FP (f n) 0, cs)]
  
after_dot f s n (c:cs)
  | isDigit c = after_dot f (s+1) (add_digit n c) cs                
after_dot f s n cs = [(FP (f n) s, cs)]
  
add_digit :: Integer -> Char -> Integer

add_digit n c =
   n * 10 + fromIntegral (fromEnum c - fromEnum '0') 
\end{code}

However, \verb|"0.1"| has no exact representation as a
Fixed\_Point Binary, nor is the natural scale what a
program necessarily needs.  What is needed is precisely
a Fixed\_Frame.

\begin{code}
readFixed :: Has_Radix t =>
             Fixed\_Frame t -> String ->
             [(Fixed_Point t, String)]

readFixed frame cs =
   [ (fix frame num,rest) | (num,read) <- readDecimalFixed cs ]
\end{code}


\end{document}
